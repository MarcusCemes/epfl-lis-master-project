<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="socketHandler">
    <Locals>
      <Local name="buffer" type="num" xsi:type="array" size="1024" />
      <Local name="bytesRead" type="num" xsi:type="array" size="1" />
      <Local name="decodeBuffer" type="num" xsi:type="array" size="3" />
    </Locals>
    <Code><![CDATA[begin
  while true
    bytesRead = sioGet(socket, buffer)

    if bytesRead > 0
      logMsg("Received " + toString("", bytesRead) + " bytes")
    endIf

    if bytesRead == -1
      // Ignore timeouts/TCP termination

    elseIf bytesRead < 2
      logMsg("[SOCKET] Too few bytes read, socket timeout?")

    elseIf buffer[0] != MAGIC_HEADER
      logMsg("[SOCKET] Missing magic header", 3)

    elseIf buffer[1] == 0 and bytesRead == 3
      logMsg("[SOCKET] Shutdown request")

      if buffer[2] == 1
        logMsg("[SHUTDOWN] Returning to start")
        resetMotion()
        enablePower()
        movej(BASE_POSE, mount, SLOW_CONFIG)
        wait(isEmpty() and isSettled())
      endIf

      // Manually create the program stop task
      taskCreate "~RemoteControl", 1, stop()
      return

    elseIf buffer[1] == 1
      logMsg("[SOCKET] Requesting motion")

      wait(!motionQueued)
      call decodeLocation(target, buffer[2])
      motionQueued = true

    else
      logMsg("[SOCKET] Unknown message")

    endIf
  endWhile
end
]]></Code>
  </Program>
</Programs>